<?xml version="1.0" encoding="utf-8"?>
<torgtaitai:CommonPopupWindow
      xmlns:mx="http://www.adobe.com/2006/mxml"
      xmlns:torgtaitai="*" 
      width="400" height="110"
      title="隠しダイスロール結果" 
      showCloseButton="true"
      close="closeWindow()"
      >
  
  <mx:Box id="secretDiceResultSpace" height="100%" width="100%">
    
  </mx:Box>
  
  <mx:Script>
    <![CDATA[
    import mx.managers.PopUpManager;
    import mx.controls.TextArea;
    import mx.controls.Button;
    import mx.containers.HBox;
    
    static private var self:SecretDiceWindow = null;
    
    static public function printMessage(data:ChatSendData):void {
        if( self == null ) {
            self = DodontoF.popup(SecretDiceWindow, false) as SecretDiceWindow;
        }
        
        self.printMessage(data)
    }
    
    static private var windowHeigthLimit:int = 450;
    static private var boxHeigth:int = 50;
    
    public function printMessage(chatSendData:ChatSendData):void {
        
        var box:HBox = new HBox();
        box.width  = this.width - 50;
        box.height  = boxHeigth;
        
        var textArea:TextArea = new TextArea()
        textArea.width = 265;
        textArea.height = boxHeigth;
        textArea.editable = false;
        textArea.text = chatSendData.getMessage();
        box.addChild(textArea);
        
        var button:Button = new Button();
        button.label = "結果公開";
        button.height = boxHeigth;
        button.addEventListener(MouseEvent.CLICK, getClickSendButtonEvent(textArea, chatSendData, box));
        box.addChild(button);
        
        secretDiceResultSpace.addChild(box);
        
        setHeight();
    }
    
    private function setHeight():void {
        secretDiceResultSpace.validateNow();
        
        var newHeight:int = secretDiceResultSpace.numChildren * (boxHeigth + 6) + 50;
        if( newHeight > windowHeigthLimit ) {
            newHeight = windowHeigthLimit;
        }
        
        this.height = newHeight;
    }
    
    private function getClickSendButtonEvent(textArea:TextArea, chatSendData:ChatSendData, box:Box):Function {
        return function(event:MouseEvent = null):void {
            
            var data:ChatSendData = new ChatSendData(chatSendData.getChannel(), chatSendData.getMessage());
            data.setDiceRollResult();
            DodontoF_Main.getInstance().getChatWindow().sendDiceRollResultMessage(data);
            
            secretDiceResultSpace.removeChild(box);
            setHeight();
        };
    }
    
    public function closeWindow():void {
        self = null;
        PopUpManager.removePopUp(this);
    }
    
    ]]>
  </mx:Script>
  
</torgtaitai:CommonPopupWindow>
