<?xml version="1.0" encoding="utf-8"?>
<torgtaitai:CommonPopupWindow
   xmlns:mx="http://www.adobe.com/2006/mxml"
   xmlns:torgtaitai="*" 
   width="300"
   height="120"
   title="セーブ" 
   showCloseButton="true"
   >
  
  <mx:Box id="loadingMessageBox" height="30" width="100%" horizontalAlign="center" verticalAlign="middle">
    <mx:Label id="loadingMessage" text="サーバのデータをファイルとして保存します。"/>
    <mx:Label id="loadingMessage2" text="" height="0" visible="false" />
  </mx:Box>
  <mx:HBox height="30" width="100%" horizontalAlign="center" verticalAlign="middle">
    <mx:Button id="readyButton" label="データ取得" click="executeServerSaveLocal()" />
    <mx:Button id="saveButton" label="ファイル保存" click="this.save()" enabled="false" />
    <mx:Button label="キャンセル" click="PopUpManager.removePopUp(this)" />
  </mx:HBox>
  
  <mx:Script>
    <![CDATA[
    import mx.containers.TitleWindow;
    import mx.managers.PopUpManager;
    import mx.utils.StringUtil;
    import mx.controls.Alert;
    
    private var saveFileName:String = "";
    private var thisObj:SaveWindow;
    
    override protected function setup():void {
        Log.loggingTuning("=>setup() Begin");
        try {
            thisObj = this;
        } catch(error:Error) {
            this.status = error.message;
        }
        Log.loggingTuning("=>setup() End");
    }
    
    protected function executeServerSaveLocal():void {
        readyButton.enabled=false;
        loadingMessage.text = "サーバデータ取得中...しばらくお待ち下さい...";
        
        executeServerSave();
    }
    protected function executeServerSave():void {
        var guiInputSender:GuiInputSender = DodontoF_Main.getInstance().getGuiInputSender();
        guiInputSender.save(this.resultFuction);
    }
    
    protected function getReadyMessage():String {
        return "セーブ(ファイル保存)準備完了";
    }
    
    protected function resultFuction(event:Object):void {
        try {
            var jsonData:Object = SharedDataReceiver.getJsonDataFromResultEvent(event);
            
            if( jsonData.result != "OK" ) {
                Alert.show(jsonData.resultText);
                return;
            }
            
            saveFileName = jsonData.saveFileName;
            Log.logging("saveFileName : " + saveFileName);
            saveButton.enabled = true;
            loadingMessage.text = getReadyMessage();
            
        } catch(error:Error) {
            this.status = error.message;
        }
    }
    
    public function save():void {
        Log.loggingTuning("=>save Begin");
        try {
            var guiInputSender:GuiInputSender = DodontoF_Main.getInstance().getGuiInputSender();
            guiInputSender.saveFileDownload(saveFileName,
                                            function():void {
                                                PopUpManager.removePopUp(thisObj);
                                            });
            saveButton.enabled = false;
            
            loadingMessage.text = "保存中…";
        } catch(error:Error) {
            this.status = error.message;
        }
        Log.loggingTuning("=>save End");
    }

    ]]>
  </mx:Script>
</torgtaitai:CommonPopupWindow>
